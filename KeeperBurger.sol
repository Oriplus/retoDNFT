//Begin
// SPDX-License-Identifier: MIT
pragma solidity 0.8.6;

import "@chainlink/contracts/src/v0.8/KeeperCompatible.sol";
import "@openzeppelin/contracts@4.6.0/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts@4.6.0/utils/Counters.sol";

contract KeeperBurger is ERC721, ERC721URIStorage, KeeperCompatibleInterface {
    using Counters for Counters.Counter;

    Counters.Counter public tokenIdCounter;
 
   // Metadata information for each stage of the NFT on IPFS.
    string[] IpfsUri = [
        "https://gateway.pinata.cloud/ipfs/QmZFBbkZ6DG7t8y5kEBHAVPW9TKkbYKUE4VNV6VmPmogQZ",
        "https://gateway.pinata.cloud/ipfs/QmeikfUWzNxic1z7Ci8SBQzUKpuFg5nD2LpktsMGasbpQV",
        "https://gateway.pinata.cloud/ipfs/QmVm8dSDkkr8Qzrs9NGxFUN7sTt2SNjZ2yyXggXHyR7mVj",
        "https://gateway.pinata.cloud/ipfs/QmbZQEwinw12JMzr6TKMmVfyPDnegKVkVodqEAuYEBsbk5",
        "https://gateway.pinata.cloud/ipfs/QmWUyyJXFWPMUVk8iaoYWAUfZdwVLWNorakmxJM6dGUX1X"
    ]; 

    uint256 lastTimeStamp;
    uint256 interval;

    constructor(uint _interval) ERC721("Delicious Burger", "DBGR") {
        interval = _interval;
        lastTimeStamp = block.timestamp;
    }

    function checkUpkeep(bytes calldata /* checkData */) external view override returns (bool upkeepNeeded, bytes memory /* performData */) {
        uint256 tokenId = tokenIdCounter.current() - 1;
        bool done;
        if (burgerStage(tokenId) >= 4) {
            done = true;
        }

        upkeepNeeded = !done && ((block.timestamp - lastTimeStamp) > interval);      
        return (upkeepNeeded, bytes(""));
        // We don't use the checkData in this example. The checkData is defined when the Upkeep was registered.
    }

    function performUpkeep(bytes calldata /* performData */) external override {
        //We highly recommend revalidating the upkeep in the performUpkeep function
        if ((block.timestamp - lastTimeStamp) > interval ) {
            lastTimeStamp = block.timestamp;            
            uint256 tokenId = tokenIdCounter.current() - 1;
            prepareBurger(tokenId);
        }
        // We don't use the performData in this example. The performData is generated by the Keeper's call to your checkUpkeep function
    }

    function safeMint(address to) public {
        uint256 tokenId = tokenIdCounter.current();
        tokenIdCounter.increment();
        _safeMint(to, tokenId);
        _setTokenURI(tokenId, IpfsUri[0]);
    }

    function prepareBurger(uint256 _tokenId) public {
        if(burgerStage(_tokenId) >= 4){return;}
        // Get the current stage of the burger and add 1
        uint256 newVal = burgerStage(_tokenId) + 1;
        // store the new URI
        string memory newUri = IpfsUri[newVal];
        // Update the URI
        _setTokenURI(_tokenId, newUri);
    }

    // determine the stage of the burger
    function burgerStage(uint256 _tokenId) public view returns (uint256) {
        string memory _uri = tokenURI(_tokenId);
        // bread
        if (compareStrings(_uri, IpfsUri[0])) {
            return 0;
        }
        // adding meat, lettuce
        if (
            compareStrings(_uri, IpfsUri[1]) 
        ) {
            return 1;
        }
        // adding cheese
        if (
            compareStrings(_uri, IpfsUri[2]) 
        ) {
            return 2;
        }
        // adding tomatoes
        if (
            compareStrings(_uri, IpfsUri[3]) 
        ) {
            return 3;
        }
        // Must be a burger
        return 4;
    }

    // helper function to compare strings
    function compareStrings(string memory a, string memory b)
        public
        pure
        returns (bool)
    {
        return (keccak256(abi.encodePacked((a))) ==
            keccak256(abi.encodePacked((b))));
    }

    // The following functions is an override required by Solidity.
    function _burn(uint256 tokenId)
        internal
        override(ERC721, ERC721URIStorage)
    {
        super._burn(tokenId);
    }

    // The following functions is an override required by Solidity.
    function tokenURI(uint256 tokenId)
        public
        view
        override(ERC721, ERC721URIStorage)
        returns (string memory)
    {
        return super.tokenURI(tokenId);
    }
}
//End
